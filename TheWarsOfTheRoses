# People born in Placentia Palace in 1491
SELECT ?person ?personLabel ?dob WHERE {
  ?person wdt:P19 wd:Q1424839.
  ?person wdt:P569 ?dob.
  FILTER(YEAR(?dob) = 1491).
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}


# Children of Edward IV and Elizabeth Woodville, with their places of birth
#defaultView:Map
SELECT ?child ?childLabel ?pob ?pobLabel ?coord
WHERE
{
# ?child  father   Edward IV
  ?child  wdt:P22 wd:Q160341;
# ?child  mother   Elizabeth Woodville
         wdt:P25 wd:Q229214;
# ?child place of birth  
         wdt:P19 ?pob.
  ?pob  wdt:P625 ?coord.
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}


# Timeline of the births of the children of Edward IV and Elizabeth Woodville
#defaultView:Timeline
SELECT ?child ?childLabel ?image ?date
WHERE
{
# ?child  father   Edward IV
  ?child  wdt:P22 wd:Q160341;
# ?child  mother   Elizabeth Woodville
         wdt:P25 wd:Q229214;
# ?child date of birth  
         wdt:P569 ?date.
  OPTIONAL {?child wdt:P18 ?image .}
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}


# Great-great-grandchildren of Edward IV and Elizabeth Woodville of each of their sons/daughters with their places of birth (if known)
#defaultView:Map
SELECT ?child ?childLabel ?grandChild ?grandChildLabel ?pob ?pobLabel ?coord
WHERE
{
# ?child  father   Edward IV
  wd:Q160341 wdt:P40 ?child.
# ?child  mother   Elizabeth Woodville
  wd:Q229214 wdt:P40 ?child.  
# ?grandchildren
  ?child wdt:P40 [wdt:P40 [wdt:P40 ?grandChild]].
# ?grandchild place of birth
OPTIONAL{
   ?grandChild wdt:P19 ?pob.
   ?pob wdt:P625 ?coord.  }
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}


# Graph of the great-great-grandchildren of John of Gaunt by his three wives
# In this case we use the "/" for concatenate levels of descendants,
# instead of the "[".
#defaultView:Graph
SELECT ?childMother ?childMotherLabel ?progeny ?progenyLabel
WHERE
{
# ?child father John of Gaunt
  wd:Q193752 wdt:P40 ?child.
# ?child mother any John of Gaunt's wives
  ?child wdt:P25 ?childMother.
# ?progeny (grandchildren)
  ?child wdt:P40/wdt:P40/wdt:P40 ?progeny.
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
ORDER BY ?childMotherLabel


# Map of the places of birth (if known) of the great-great-grandchildren of John of Gaunt by his three wives
#defaultView:Map
SELECT ?childMother ?childMotherLabel ?progeny ?progenyLabel ?pob ?pobLabel ?coord
WHERE
{
# ?child father John of Gaunt
  wd:Q193752 wdt:P40 ?child.
# ?child mother any John of Gaunt's wives
  ?child wdt:P25 ?childMother.
# ?progeny (grandchildren)
  ?child wdt:P40/wdt:P40/wdt:P40 ?progeny.
OPTIONAL{
   ?progeny wdt:P19 ?pob.
   ?pob wdt:P625 ?coord.  }
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
ORDER BY ?childMotherLabel


# Number the great-great-grandchildren of John of Gaunt by his three wives
SELECT ?childMother ?childMotherLabel (COUNT(?progeny) AS ?numprogeny)
WHERE
{
# ?child father John of Gaunt
  wd:Q193752 wdt:P40 ?child.
# ?child mother any John of Gaunt's wives
  ?child wdt:P25 ?childMother.
# ?progeny (grandchildren)
  ?child wdt:P40/wdt:P40/wdt:P40 ?progeny.
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
GROUP BY ?childMother ?childMotherLabel
ORDER BY DESC(?numprogeny)


# Descendants of John de Gaunt sorted by descending date of birth
SELECT ?descendant ?descendantLabel ?dob
WHERE
{
  wd:Q193752 wdt:P40+ ?descendant.
  ?descendant wdt:P569 ?dob
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
ORDER BY ASC(?dob)


# Number of descendants of John de Gaunt sorted by descending date of birth
SELECT (COUNT(?descendant) AS ?numDescendants)
WHERE
{
  wd:Q193752 wdt:P40+ ?descendant.
  ?descendant wdt:P569 ?dob
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}


#Map with the places of birth of all the descendants of John de Gaunt to present day
#defaultView:Map
SELECT ?descendant ?descendantLabel ?dob ?pob ?pobLabel ?coord
WHERE
{
  wd:Q193752 wdt:P40+ ?descendant.
  ?descendant wdt:P569 ?dob
  OPTIONAL{
   ?descendant wdt:P19 ?pob.
   ?pob wdt:P625 ?coord.  }
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
ORDER BY ASC(?dob)
